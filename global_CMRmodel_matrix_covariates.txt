
   
     model {
    
      #############################################################
      #                                                           #
      #                  Biological process                       #
      #                                                           #
      #############################################################
      
      # prior to describe the full community size
      #omega~dunif(0,1)
      omega ~ dbeta(0.001,1)
      
      # superpopulation process
      for (g in 1:ngen) {
        
        w[g]~dbern(omega)
        
      }   
      
      # Site Occupancy Dynamics (Priors)
       
       
        # ----------------------
        #     Gamma (origination)
        # ----------------------
        # intercepts
        gamma.u ~ dunif(0,1) # range origination
        intercept.gamma <- logit(gamma.u) # intercept origination
        # regression coeff
        beta.gamma.prec ~ dunif(-20,20)# dnorm(0,1/3^2)# precipitation
        beta.gamma.temp ~ dunif(-20,20)#dnorm(0,1/3^2)# temperature
        beta.gamma.area ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        beta.gamma.coast ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        
        # tropics
        beta.gamma.area.t ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        beta.gamma.coast.t ~ dunif(-20,20)#dnorm(0,1/3^2) # area
     
        # ----------------------
        #     Phi (persistence)
        # ----------------------
        # intercepts
        phi.u ~ dunif(0,1) # range persistence
        intercept.phi <- logit(phi.u) # intercept persistence
        # regression coeff
        beta.phi.prec ~ dunif(-20,20)#dnorm(0,1/3^2)# precipitation
        beta.phi.temp ~ dunif(-20,20)#dnorm(0,1/3^2)# temperature
        beta.phi.area ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        beta.phi.coast ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        
        # tropics
        beta.phi.area.t ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        beta.phi.coast.t ~ dunif(-20,20)#dnorm(0,1/3^2) # area
        
        ## set initial conditions for occupancy of each genus
        psi1 ~ dunif(0,1)
        
           
       ############      Model       #############
       
       # model for phi and gamma
       ### model dynamic parameters
        
        #for (t in 2:nint){
        for(t in 1:(nint-1)){
        
             # speciation
             logit(gamma[t]) <-  intercept.gamma + 
                                   #beta.gamma.prec*precipitation[t]+
                                   beta.gamma.temp*temperature[t]+
                                   beta.gamma.area*area[t]+
                                   #beta.gamma.coast*coast[t]+
                                   beta.gamma.area.t*area.t[t]#+
                                   #beta.gamma.coast.t*coast.t[t]
                                   
                                   
                                         
              # persistence
              logit(phi[t]) <-  intercept.phi + 
                                  beta.phi.prec*precipitation[t]+
                                  beta.phi.temp*temperature[t]+
                                  beta.phi.area*area[t]+
                                  beta.phi.coast*coast[t]+
                                  beta.phi.area.t*area.t[t]+
                                  beta.phi.coast.t*coast.t[t]
        }
        
       
                       
      # occupancy dynamics
       
        for (g in 1:ngen) {
         
            z[g,1]~dbern(psi1) # occupancy status initialization
      
                for (t in 2:nint){
              
                  # model likelihood
                  ### modeling dynamics conditional on previous time realized occurrence z
                  muZ[g,t] <- z[g,t-1] *  phi[t-1] + ### if occupied, p of not getting extinct/persist in the next time
                                (1-z[g,t-1]) *  gamma[t-1] ###  if not occupied, p of originate in the next time
                  
                 # realized occurrence
                 muZW[g,t] <- muZ[g,t]*w[g]
          		   z[g,t] ~ dbern(muZW[g,t])
              
          }#t
        
        } #g
  
  
    #############################################################
    #                                                           #
    #         Observation process across formations             #
    #                                                           #
    #############################################################
    
    ###  detection intercept
    # intercept    
    p.u ~ dunif(0,1) # range origination
    intercept.p <- logit(p.u) # intercept origination
    # regression coef
    beta.p.lat ~ dunif(-20,20)#dnorm(0,1/3^2)
    beta.p.range ~ dunif(-20,20)#dnorm(0,1/3^2)
    beta.p.time ~ dunif(-20,20)#dnorm(0,1/3^2)
    beta.p.temp ~ dunif(-20,20)#dnorm(0,1/3^2)
     
    # observation submodel
    for (g in 1:ngen) { ## loop over observations 
      
      for (t in 1:nint) { ## loop over observations 
    
          # p independent on the number of formations
          logit(p[g,t])<- intercept.p+
                          beta.p.lat*lat_obs[g]+
                          beta.p.range*range[g]+
                          beta.p.time*time[t]+
                          beta.p.temp*temp_obs[g]
            
            
          # observation
          # Specify the binomial observation model conditional on occupancy state
          y[g,t] ~ dbin(muY[g,t], nform[t])
          muY[g,t] <- z[g,t]*p[g,t]
                  
        }
      
    }
      
    # derived parameters
    for (t in 1:nint){
        SRexp[t] <- sum (z[,t])
      }
    
    # total metacommunity size
    Ntotal <- sum(w[])
    
    
    }## end of the model
    
    
