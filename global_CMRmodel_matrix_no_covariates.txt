
   
    
     model {
    
    
      #############################################################
      #                                                           #
      #                  Biological process                       #
      #                                                           #
      #############################################################
      
      # prior to describe the full community size
      #omega~dunif(0,1)
      omega ~ dbeta(0.001,1)
      
      # superpopulation process
      for (g in 1:ngen) {
        
        w[g]~dbern(omega)
        
      }
      
       # Site Occupancy Dynamics (Priors)
       # intercepts
       # gamma
       for(t in 1:(nint-1)){
       
         phi[t]~dunif(0,1)
         gamma[t]~dunif(0,1)
         
       }
       
       ## set initial conditions for each genus
       psi1 ~ dunif(0,1)
     
   ############      Model       #############
   
   # model for phi and gamma
   ### model dynamic parameters
    
    for (g in 1:ngen) {
         
            z[g,1]~dbern(psi1) # occupancy status initialization
      
                for (t in 2:nint){
      
                  # model likelihood
                  ### modeling dynamics conditional on previous time realized occurrence z
                  muZ[g,t] <- z[g,t-1] *  phi[t-1] + ### if occupied, p of not getting extinct/persist in the next time
                              (1-z[g,t-1]) *  gamma[t-1] ###  if not occupied, p of originate in the next time
                  
                 # realized occurrence
                 muZW[g,t] <- muZ[g,t]*w[g]
          		   z[g,t] ~ dbern(muZW[g,t])
              
          }#t
        
         
        
        } #g
    
  
    #############################################################
    #                                                           #
    #         Observation process across formations             #
    #                                                           #
    #############################################################
    
    # Priors for detection probability
    ###  detection intercept and coefficients
    
    for (t in 1:nint){
      p[t] ~ dunif(0,1)
    }
    
    # observation submodel
     for (g in 1:ngen) {
      for (t in 1:nint){
    
            # observation
            # Specify the binomial observation model conditional on occupancy state
            y [g,t] ~ dbin(muY[g,t],nform[t])
            muY[g,t] <- z[g,t]*p[t]      
                  
                }
        }
      
     
      
    # derived parameters
    for (t in 1:nint){
        SRexp[t] <- sum (z[,t])
      }
    
    # total metacommunity size
    Ntotal <- sum(w[])
    
    
    }## end of the model
    
    
