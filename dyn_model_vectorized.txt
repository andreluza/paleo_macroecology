
    model {
    
    #############################################################
    #                                                           #
    #                  Biological process                       #
    #                                                           #
    #############################################################
    
    
    # Priors
    # 10    
    for (t in 2:nint) {
    
       for (g in 1:ngenus) {
    
        
          ## colonization (origination)
          gamma [t,g] ~ dunif (0,1)
          #gamma [g] ~ dunif (0,1)
          
          ## persistence probability
          phi [t,g] ~ dunif (0,1)
          #phi [g] ~ dunif (0,1)
        }
       
       }
  
    
    ## set initial conditions
    ## priors for occupancy in time 1
     for (g in 1:ngenus) {
     
          psi1 [g] ~ dunif (0,1)
     # 33
     }
    
    ############ Model ########################
    for (g in 1:ngenus) {
    
        z[1,g]~dbern(psi1[g]) # occupancy status initialization
    
            for (t in 2:nint){
            
              # model likelihood
              ### modeling dynamics conditional on previous time realized occurrence z
              muZ[t,g] <- z[t-1,g] *  phi[t,g] + ### if occupied, p of not getting extinct/persist in the next time
                          (1-z[t-1,g]) *  gamma[t,g] ###  if not occupied, p of originate in the next time
                        
            z[t,g] ~ dbern(muZ[t,g])
    
        }#t # 50
    }#i
    
    #############################################################
    #                                                           #
    #         Observation process across formations             #
    #                                                           #
    #############################################################
    
    # priors
    ## detection
    
    for (g in 1:ngenus) {
    
            p[g] ~ dunif (0,1)

      } 
    # 68
    ### model
    for (k in 1:nobs){
        
        y [k] ~ dbern (muY[form[k],int[k],genus[k]])
        muY [form[k],int[k],genus[k]] <- z [int[k],genus[k]] * p[genus[k]]
        
    }
    
    
    # -----------------------------------------------
    
    ## derived parameters
    # number of genus per interval
    for (t in 1:nint) {
        Ngen[t]<-sum(z[t,])
    }
    
    # average persistence and origination
    #for (g in 1:ngenus) {
    #  avphi[g] <- mean(phi[2:nint,g])
    #  avgamma[g]<- mean(gamma[2:nint,g])
    #}
      
    # turnover (proportional gain or loss)
    for (t in 2:nint) {  
      
        propcH [t] <-(sum(z[t,])-sum (z[t-1,]))/sum(z[t-1,]) 
      
    }
    
    # equilibrium occupancy (which genus decline or increase over time)
    for (g in 1:ngenus) {
    
        psi.eq[g] <- mean(gamma[2:nint,g])/(mean(gamma[2:nint,g])+mean(1-phi[2:nint,g])) # Equilibrium occupancy
        #psi.eq[g] <- gamma[g]/(gamma[g]+(1-phi[g])) # Equilibrium occupancy
    }
    
    # relative extinction rate (μ/λ; Rabosky 2018) of each time
    for (t in 2:nint) { 
     for (g in 1:ngenus) {
    
        #RER[g] <- (1-phi[g])/gamma[g]
        RER[t,g] <- (1-phi[t,g])/gamma[t,g]
    
      }
	}
    
    # net diversification rate (r= μ - λ; Rabosky 2018) of each time
    for (t in 2:nint) { 
     for (g in 1:ngenus) {
    
        #R0[g] <- (1-phi[g])-gamma[g]
        R0[t,g] <- (1-phi[t,g])-gamma[t,g]
    
     }
    }
    
        
    }## end of the model
    
    
