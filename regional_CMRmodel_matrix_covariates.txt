
   
     
     model {
    
    
      #############################################################
      #                                                           #
      #                  Biological process                       #
      #                                                           #
      #############################################################
      
       # Site Occupancy Dynamics (Priors)
       
        # ----------------------
        #     Gamma (origination)
        # ----------------------
        # intercepts
        gamma.u ~ dunif(0,1) # range origination
        intercept.gamma <- logit(gamma.u) # intercept origination
        # regression coeff
        beta.gamma.prec ~ dnorm(0, 0.01)# precipitation
        beta.gamma.temp ~ dnorm(0, 0.01)# temperature
        #beta.gamma.lat ~ dnorm(0,0.01) # latitude
        beta.gamma.area ~ dnorm(0,0.01) # latitude
        
        # ----------------------
        #     Phi (persistence)
        # ----------------------
        # intercepts
        phi.u ~ dunif(0,1) # range persistence
        intercept.phi <- logit(phi.u) # intercept persistence
        # regression coeff
        beta.phi.prec ~ dnorm(0, 0.01)# precipitation
        beta.phi.temp ~ dnorm(0, 0.01)# temperature
        #beta.phi.lat ~ dnorm(0,0.01)
        beta.phi.area ~ dnorm(0,0.01)
        
       
        ## set initial conditions for the whole system
        psi1 ~ dunif(0,1)
        
        # initial state
        for (g in 1:ngen) {
          for (i in 1:nsites) {
          
             z[g,1,i]~dbern(psi1) # occupancy status initialization
                  
          }
        }
    
    ############      Model       #############
   
   # occupancy dynamics
    
     for (i in 1:nsites) {
       for (t in 2:nint){
    
            # origination
            logit(gamma[t,i]) <-  intercept.gamma + 
                                    beta.gamma.prec*precipitation[t,i]+
                                    beta.gamma.temp*temperature[t,i]+
                                    #beta.gamma.lat*lat[i]+
                                    beta.gamma.area*area[t,i]
            # persistence
            logit(phi[t,i]) <-    intercept.phi + 
                                    beta.phi.prec*precipitation[t,i]+
                                    beta.phi.temp*temperature[t,i]+
                                    #beta.phi.lat*lat[i]+
                                    beta.phi.area*area[t,i]
    
            # dynamics across genus
              
            for (g in 1:ngen) {
                    
                # model likelihood
                ### modeling dynamics conditional on previous time realized occurrence z
                muZ[g,t,i] <- z[g,t-1,i] *  phi[t,i] + ### if occupied, p of not getting extinct/persist in the next time
                              (1-z[g,t-1,i]) *  gamma[t,i] ###  if not occupied, p of originate in the next time
                
               # realized occurrence
        		   z[g,t,i] ~ dbern(muZ[g,t,i])
            
             }#gen
            } #stages
          }#i sites
      
    
    #############################################################
    #                                                           #
    #         Observation process across formations             #
    #                                                           #
    #############################################################
    
    # Priors for detection probability
    # intercept    
    p.u ~ dunif(0,1) # range origination
    intercept.p <- logit(p.u) # intercept origination
    
    # regression coef
    beta.p.lat ~ dnorm(0,0.01)
    beta.p.range ~ dnorm(0,0.01)
    beta.p.time ~ dnorm(0,0.01)
    beta.p.temp ~ dnorm(0,0.01)
        
     
    # observation submodel
    for (g in 1:ngen) { ## loop over observations 
      
      for (t in 1:nint) { ## loop over observations 
    
          # p independent on the number of formations
          logit(p[g,t])<- intercept.p+
                          beta.p.lat*lat_obs[g]+
                          beta.p.range*range[g]+
                          beta.p.time*time[t]+
                          beta.p.temp*temp_obs[g]
            
          for (i in 1:nsites) {  
      
              # observation process
              # Specify the binomial observation model conditional on occupancy state
              y[g,t,i] ~ dbin(muY[g,t,i], nform[t,i])
              muY[g,t,i] <- z[g,t,i]*p[g,t]
                      
        }
      
      }
    
    }
    
    # derived parameters
    # true richness
    for (t in 1:nint){
       for (i in 1:nsites) {
       
         SRexp[t,i] <- sum (z[,t,i]) # true richness estimate
         
    
    }
   }
 
    
        
    } ## end of the model
    
    
