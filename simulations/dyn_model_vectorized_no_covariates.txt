
   
   model {
    
     #############################################################
    #                                                           #
    #                  Biological process                       #
    #                                                           #
    #############################################################
    
    
    # Site Occupancy Dynamics (Priors)
    
   gamma~dunif(0,1)
   phi~dunif(0,1)
    
   ## set initial conditions
   psi1~dunif(0,1)
       
   ############      Model       #############
   
   
   for (i in 1:nsites) {

    for (s in 1:nspp) {
    
          z[i,1,s]~dbern(psi1) # occupancy status initialization
    
              for (t in 2:nint){
            
                # model likelihood
                ### modeling dynamics conditional on previous time realized occurrence z
                muZ[i,t,s] <- z[i,t-1,s] *  phi + ### if occupied, p of not getting extinct/persist in the next time
                          (1-z[i,t-1,s]) *  gamma ###  if not occupied, p of originate in the next time
                
        # realized occurrence
		    z[i,t,s] ~ dbern(muZ[i,t,s])
    
        }#t
      } #spp
     }#i
    
    #############################################################
    #                                                           #
    #         Observation process across formations             #
    #                                                           #
    #############################################################
    
    
    
        # Priors for detection probability
        p~dunif(0,1)
        

     ############      Model       #############
     
     
     # observation submodel
     
     for (k in 1:nobs) { ## loop over observations
                            
               y [k] ~ dbern(muY[site[k],survey[k],int[k],spp[k]])
               # Specify the observation model conditional on occupancy state
               muY [site[k],survey[k],int[k],spp[k]] <- z[site[k],int[k],spp[k]] * p
                
                }
      
        
    }## end of the model
    
    
    
    
